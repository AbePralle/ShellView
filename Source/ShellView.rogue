#================================================================================
# ShellView.rogue
# November 14, 2021
#================================================================================
$define VERSION "ShellView v1.0"
$define AUTHOR "November 17, 2021 by Abe Pralle"

$requireRogue "1.9.6"

$include "ANSIBitmap.rogue"
$include "ANSIColor.rogue"
$include "ImageView.rogue"
$include "RGB.rogue"
$include "ShellViewUI.rogue"

uses Bitmap
uses UI/ConsoleUI
uses Utility/CommandLineParser

try
  ShellView.init( System.command_line_arguments )
catch (error:Error)
  Console.error.println error
  System.exit 1
endTry

class ShellView [singleton]
  PROPERTIES
    command : Value

  METHODS
    method init( args:String[] )
      command = parse_args( args )

      if (command//options//help or command//args.count == 0)
        print_usage
        System.exit 0
      endIf

      # Process wildcards in filepaths (especially Windows)
      forEach (filepath in command//args.to_list<<String>>)
        if (filepath.contains('*') or filepath.contains('?') or File.is_folder(filepath))
          ShellViewUI.filepaths.add( File.listing(filepath) )
        else
          ShellViewUI.filepaths.add( filepath )
        endIf
      endForEach

      if (command//options//print)
        loop (ShellViewUI.filepaths.count)
          ShellViewUI.image_view.next_image
          ShellViewUI.update( Console.bounds )
          ShellViewUI.display
          ConsoleStyle.reset
          Console.clear_to_eol
          println
        endLoop
      else
        ShellViewUI.image_view.next_image
        ShellViewUI.run
      endIf

    method parse_args( args:String[] )->Value
      local command = CommandLineParser().
      [
        option( "--dither",    &alias="-d" )
        option( "--help",      &aliases=["-h","-?"] )
        option( "--print",     &alias="-p" )
      ].parse( args )

      return command

    method print_usage
      println VERSION
      println AUTHOR
      println
      println @|USAGE
               |  shellview [OPTIONS] [image-filepaths...]
               |
               |COMMANDS (INTERACTIVE MODE)
               |  I, O, LEFT/RIGHT CLICK, SCROLL WHEEL
               |    Zoom in or out.
               |
               |  WASD, HJKL, ARROW KEYS, MOUSE DRAG
               |    Scroll around image if zoomed in.
               |    Cycle next/previous image if zoomed out (A/D, H/L, LEFT/RIGHT).
               |
               |  N, B or C, Z
               |    Cycle to (N)ext image or (B)ack to previous image.
               |    Alternately, (C)ontinue to next or (Z)=back.
               |
               |  R
               |    Toggle color dithe(R)ing.
               |
               |  Q, ESCAPE
               |    Quit ShellView.
               |
               |OPTIONS
               |  --dither, -d
               |    Start with color dithering turned on (toggle with R).
               |
               |  --help, -h, -?
               |    Show this help text.
               |
               |  --print, -p
               |    Print all images to the console (scaled to fit) without entering interactive mode.
endClass

