#================================================================================
# ShellView.rogue
# November 14, 2021
#================================================================================

$requireRogue "1.9.6"

$include "ANSIBitmap.rogue"
$include "ANSIColor.rogue"
$include "ImageView.rogue"
$include "RGB.rogue"
$include "ShellViewUI.rogue"

uses Bitmap
uses UI/ConsoleUI
uses Utility/CommandLineParser

try
  ShellView.init( System.command_line_arguments )
catch (error:Error)
  Console.error.println error
  System.exit 1
endTry

class ShellView [singleton]
  PROPERTIES
    command : Value

  METHODS
    method init( args:String[] )
      command = parse_args( args )

      if (command//options//help or command//args.count == 0)
        print_usage
        System.exit 0
      endIf

      ShellViewUI.filepaths = command//args.to_list<<String>>
      ShellViewUI.filepaths.add( ShellViewUI.filepaths.remove_first )
      ShellViewUI.image_view.set_image( ShellViewUI.filepaths.last )

      if (command//options//print)
        loop (ShellViewUI.filepaths.count)
          ShellViewUI.update( Console.bounds )
          ShellViewUI.display
          ConsoleStyle.reset
          Console.clear_to_eol
          println
          ShellViewUI.image_view.next_image
        endLoop
      else
        ShellViewUI.run
      endIf

    method parse_args( args:String[] )->Value
      local command = CommandLineParser().
      [
        option( "--dither",    &alias="-d" )
        option( "--help",      &aliases=["-h","-?"] )
        option( "--print",     &alias="-p" )
      ].parse( args )

      return command

    method print_usage
      println @|USAGE
               |  shellview [OPTIONS] [image-filepaths...]
               |
               |COMMANDS (INTERACTIVE MODE)
               |  I, O, LEFT/RIGHT CLICK, SCROLL WHEEL
               |    Zoom in or out.
               |
               |  WASD, ARROWS
               |    Scroll around image if zoomed in or cycle next/previous image if zoomed out.
               |
               |  N, B
               |    Cycle to (N)ext image or (B)ack to previous image.
               |
               |  Q, ESCAPE
               |    Quit ShellView.
               |
               |OPTIONS
               |  --dither, -d
               |    Enable color dithering.
               |
               |  --help, -h, -?
               |    Show this help text.
               |
               |  --print, -p
               |    Print all images to the console (scaled to fit) without entering interactive mode.
endClass

