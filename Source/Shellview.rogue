#================================================================================
# Shellview.rogue
# November 14, 2021
#================================================================================
$define VERSION "Shellview v1.1.2"
$define DATE "December 26, 2021"

$requireRogue "2.0.10"

$include "ANSIBitmap.rogue"
$include "ANSIColor.rogue"
$include "ImageView.rogue"
$include "RGB.rogue"
$include "ShellViewUI.rogue"

uses Bitmap
uses Bitmap/IO
uses Console/CommandLineParser
uses UI/ConsoleUI

try
  Shellview.init( System.command_line_arguments )
catch (error:Error)
  Console.error.println error
  System.exit 1
endTry

class Shellview [singleton]
  PROPERTIES
    command : Value

  METHODS
    method init( args:String[] )
      command = parse_args( args )

      if (command//options//help or command//args.count == 0)
        print_usage
        System.exit 0
      endIf

      # Process wildcards in filepaths (especially Windows)
      forEach (filepath in command//args.to_list<<String>>)
        if (filepath.contains('*') or filepath.contains('?') or File(filepath).is_folder)
          ShellViewUI.filepaths.add( File(filepath).listing )
        else
          ShellViewUI.filepaths.add( filepath )
        endIf
      endForEach

      if (command//options//print)
        loop (ShellViewUI.filepaths.count)
          ShellViewUI.image_view.next_image
          ShellViewUI.update( Box(Console.width,Console.height) )
          ShellViewUI.display
          ConsoleStyle.reset
          Console.clear_to_eol
          println
        endLoop
      else
        ShellViewUI.image_view.next_image
        ShellViewUI.run
      endIf

    method parse_args( args:String[] )->Value
      local command = CommandLineParser().
      [
        option( "--4-bit",     &alias="-4" )
        option( "--dither",    &alias="-d" )
        option( "--grayscale", &alias="-g" )
        option( "--help",      &aliases=["-h","-?"] )
        option( "--print",     &alias="-p" )
      ].parse( args )

      return command

    method print_usage
      println "Shellview v$"(VERSION)
      println "$ by Abe Pralle"(DATE)
      println
      println @|USAGE
               |  shellview [OPTIONS] [image-filepaths...]
               |
               |COMMANDS (INTERACTIVE MODE)
               |  I, O, LEFT/RIGHT CLICK, SCROLL WHEEL
               |    Zoom in or out.
               |
               |  WASD, HJKL, ARROW KEYS, MOUSE DRAG
               |    Scroll around image if zoomed in.
               |    Cycle next/previous image if zoomed out (A/D, H/L, LEFT/RIGHT).
               |
               |  N, B or C, Z
               |    Cycle to (N)ext image or (B)ack to previous image.
               |    Alternately, (C)ontinue to next or (Z)=back.
               |
               |  G
               |    Toggle (G)rayscale mode.
               |
               |  R
               |    Toggle color dithe(R)ing.
               |
               |  Q, ESCAPE
               |    Quit Shellview.
               |
               |OPTIONS
               |  --4-bit, -4
               |    Use 4-bit ANSI color (16 possible colors).
               |
               |  --dither, -d
               |    Start with color dithering turned on (toggle with R).
               |
               |  --grayscale, -g
               |    Start in grayscale mode (toggle with G).
               |
               |  --help, -h, -?
               |    Show this help text.
               |
               |  --print, -p
               |    Print all images to the console (scaled to fit) without entering interactive mode.
endClass

